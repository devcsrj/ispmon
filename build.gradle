plugins {
  id "application"
  id "com.github.johnrengelman.shadow" version "4.0.3"
  id "org.jetbrains.kotlin.jvm" version "1.3.50"
  id "com.moowork.node" version "1.2.0"
  id 'com.palantir.docker' version '0.22.1'
}

repositories {
  mavenCentral()
  mavenLocal()
  jcenter()
  maven {
    url = "https://dl.bintray.com/spekframework/spek/"
  }
}

ext {
  graalVersion = "19.2.0"
  junitVersion = "5.3.2"
  kluentVersion = "1.53"
  kotlinVersion = "1.3.50"
  spekVersion = "2.0.6"
  vertxVersion = "3.8.1"

  mainVerticleName = "com.github.devcsrj.ispmon.Ispmon"
  watchForChange = "src/**/*.kt"
  doOnChange = "${projectDir}/gradlew classes"

  nativeImageArgs = [
    "--report-unsupported-elements-at-runtime",
    "--allow-incomplete-classpath",
    "-J-Djava.security.properties=java.security",
    "--no-server",
  ]
}

dependencies {
  compileOnly("com.oracle.substratevm:svm:$graalVersion")

  implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
  implementation("com.fasterxml.jackson.module:jackson-module-kotlin:2.9.9")
  implementation("io.vertx:vertx-config:$vertxVersion")
  implementation("io.vertx:vertx-core:$vertxVersion")
  implementation("io.vertx:vertx-web:$vertxVersion")

  // Replace SunEC provider - https://github.com/oracle/graal/issues/951
  implementation "org.bouncycastle:bcprov-jdk15on:1.61"
  implementation "org.bouncycastle:bcpkix-jdk15on:1.61"

  testImplementation("org.spekframework.spek2:spek-dsl-jvm:$spekVersion")
  testImplementation("io.vertx:vertx-junit5:$vertxVersion")
  testImplementation("io.vertx:vertx-web-client:$vertxVersion")
  testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
  testImplementation("org.amshove.kluent:kluent:$kluentVersion")
  testImplementation("com.squareup.okhttp3:mockwebserver:4.1.0")

  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
  testRuntimeOnly("org.spekframework.spek2:spek-runner-junit5:$spekVersion")
  testRuntimeOnly("org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion")
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
}

application {
  mainClassName = "io.vertx.core.Launcher"
}

tasks {
  test {
    useJUnitPlatform() {
      includeEngines("spek2")
    }
  }

  compileKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  compileTestKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }

  run {
    args = ['run', mainVerticleName,
            "--redeploy=$watchForChange",
            "--launcher-class=$mainClassName",
            "--on-redeploy=$doOnChange"]
  }

  shadowJar {
    classifier = 'fat'
    manifest {
      attributes.put("Main-Verticle", mainVerticleName)
    }
    mergeServiceFiles {
      include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
  }

  node {
    version = '8.11.1'
    yarnVersion = '1.12.3'
    download = true
    nodeModulesDir = file("${project.projectDir}/src/main/frontend")
  }

  processResources {
    from('src/main/frontend/dist/') {
      into 'static'
    }
  }

  build.dependsOn yarn_build

  task nativeImage(type: Exec) {
    group = "build"
    description = "Compiles a native image for your operating system"
    commandLine = "native-image"
    args = nativeImageArgs + [
      "-jar",
      shadowJar.outputs.files.singleFile,
      "ispmon"
    ]
    dependsOn(shadowJar)
    doLast {
      logger.info("You can now execute: ./ispmon")
    }
  }

  task staticNativeImage(type: Exec) {
    group = "build"
    description = "Builds a unix-compatible native image"
    commandLine = "docker"
    args = [
      "run",
      "-v",
      "${shadowJar.outputs.files.singleFile.parent}:/project",
      "--rm",
      "devcsrj/graal-native-image:$graalVersion",
    ] + nativeImageArgs + [
      "--static",
      "-jar",
      shadowJar.outputs.files.singleFile.name,
      "ispmon"
    ]
    dependsOn(shadowJar)
  }

  docker {
    name "devcsrj/ispmon:${version}"
    dockerfile file("src/bin/launcher/Dockerfile")
    files file(shadowJar.outputs.files.singleFile.parent + "/ispmon")

    dependsOn(staticNativeImage)
  }
}
